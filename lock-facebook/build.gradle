apply plugin: 'com.auth0.gradle.oss-library.android'

logger.lifecycle("Using version ${version} for ${name}")

oss {
    name 'Lock-Facebook.Android'
    repository 'Lock-Facebook.Android'
    organization 'auth0'
    description 'Lock for Android add-on to use Facebook Android SDK'

    developers {
        auth0 {
            displayName = 'Auth0'
            email = 'oss@auth0.com'
        }
        lbalmaceda {
            displayName = 'Luciano Balmaceda'
            email = 'luciano.balmaceda@auth0.com'
        }
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            // Configure whether failing tests should fail the build
            ignoreFailures false

            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
            }
        }
    }

    lintOptions {
        lintConfig file('../gradle/lint.xml')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.auth0.android:auth0:1.8.0'
    compile 'com.facebook.android:facebook-android-sdk:4.22.0'
    testCompile('junit:junit:4.11') {
        exclude module: 'hamcrest-core'
    }
    testCompile 'org.robolectric:robolectric:3.1.2'
    testCompile 'org.hamcrest:hamcrest-integration:1.3'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile('com.squareup:fest-android:1.0.+') {
        exclude group: 'com.android.support', module: 'support-v4'
    }
}